plugins {
	id 'org.springframework.boot' version '2.7.6'
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
}

group = 'org.artemnizhnyk'
version = '1.0'

/**
 * General dependencies to all modules
 */
subprojects {

	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	configurations {
		provided
	}

	sourceSets {
		main.compileClasspath += configurations.provided
		test.compileClasspath += configurations.provided
		test.runtimeClasspath += configurations.provided
	}

	sourceCompatibility = JavaVersion.VERSION_17

	jar {
		enabled = true
	}

	test {
		useJUnitPlatform()
	}

	repositories {
		mavenCentral()
	}

	/**
	 * Lombok
	 */
	dependencies {
		compileOnly 'org.projectlombok:lombok:1.18.24'
		testCompileOnly 'org.projectlombok:lombok:1.18.24'
		annotationProcessor 'org.projectlombok:lombok:1.18.24'
		testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
	}

	/**
	 * Logging
	 */
	dependencies {
		implementation 'org.apache.logging.log4j:log4j-api:2.18.0'
		implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.18.0'
	}

	/**
	 * Test
	 */
	dependencies {
		testImplementation 'org.springframework.security:spring-security-test'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'com.h2database:h2'
	}
}